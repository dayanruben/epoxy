// Sets up publishing of release artifacts.
// Note: Keep this script in sync across all airbnb open source projects.
apply plugin: 'com.vanniktech.maven.publish'


// https://vanniktech.github.io/gradle-maven-publish-plugin/other/
publishing {
    repositories {
        maven {
            // The "name" value creates a task like `publishAllPublicationsTo[Name]Repository
            // In this case, publishAllPublicationsToAirbnbArtifactoryRepository
            name = 'airbnbArtifactory'
            url = version.toString().endsWith("SNAPSHOT") ? findProperty("ARTIFACTORY_SNAPSHOT_URL") : findProperty("ARTIFACTORY_RELEASE_URL")
            credentials {
                username = findProperty("ARTIFACTORY_USERNAME")
                password = findProperty("ARTIFACTORY_PASSWORD")
            }
        }
    }
}

// Apply conditional signing logic by setting the property at configuration time
// This must happen before the mavenPublishing plugin reads the property
if (findProperty("doNotSignRelease").toString().toBoolean()) {
    println("Skipping release signing")
    // Override the signAllPublications property to disable signing
    ext.set("signAllPublications", "false")
} else {
    println("Signing release with gpg")
    // signAllPublications is already set to true in gradle.properties
}